file = _{ SOI ~ (cell | text)* ~ EOI }

text = { "" ~ (!cell ~ ANY)+ ~ "" } 

cell = _{ (function_cell | visible_cell | hidden_cell) }

// TODO: allow function cells to be declared in line
function_cell = { "{{" ~ cell_key ~ ";" ~ args ~ ";" ~ expr ~ "}}" ~ ("\r\n" | "\n") }

visible_cell = { "{{" ~ cell_key ~ ";" ~ expr ~ "}}" }

hidden_cell = { "{{" ~ "!" ~ cell_key ~ ";" ~ expr ~ "}}" }

cell_key = { ident }

args = { (ident ~ ","?)* }

expr = { term ~ (infix ~ term)* }

infix = _{
	  add 
	| subtract 
	| multiply
	| divide 
	| power 
	| modulo 
	}

	add = { "+" } 
	subtract = { "-" }
	multiply = { "*" }
	divide = { "/" }
	power = { "^" }
	modulo = { "%" }

// TODO: fix factorial
// term = _{ prefix* ~ primary ~ postfix* }
term = _{ prefix* ~ primary }

prefix = _{ negative }
	negative = { "-" }

postfix = _{ factorial }
	factorial = { "!" }

primary = _{ func | "(" ~ expr ~ ")" | number | ident  }

func = { ident ~ "(" ~ expr* ~ ")" }

number = @{ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)? }

ident = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }

WHITESPACE = _{ " " | "\t" }